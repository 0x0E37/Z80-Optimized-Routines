;NOTE: This routine may be under a different License.

;created by KermMartian, based off code by SirCmpwn (Drew DeVault)
;Optimized by Zeda
;Gets an accurate battery level on non-TI-83+ devices.

; Outputs:   B: Value from 0-4 indicating battery level (0 is critical)
; Outputs:   B: Value from 0-4 indicating battery level (0 is critical)
GetBatteryLevel:
   push af
   in a, (2)
   rlca ;Roll bit 7 into carry.
   jr c, GetBatteryLevel_Not3Plus
   in a, (2)
   and 1
   ld b, a
   pop af
   ret
GetBatteryLevel_Not3Plus:
   ld b,3
GetBatteryLevelLoop:
   ld a,b
   rrca
   rrca
   and $C0
     push hl    ; Wasting clock cycles
     inc hl     ; Not sure if needed
     dec hl     ; I didn't write the routine
     pop hl     ; Just making it smaller.
   or %00000110
   out (6), a
   in a, (2)
   bit 0, a
   jr z, GetBatteryLevel_Done
   djnz GetBatteryLevelLoop
   ld b,4
GetBatteryLevel_Done:
   ld a, %110
   out (6), a
   pop af
   ret
