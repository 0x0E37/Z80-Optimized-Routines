#include "ti8x/gfx/bigsprite_subroutine.z80"
bigsprite_Erase:
;Inputs:
;     B is the X-coordinate
;     C is the Y-Coordinate
;     DE points to the sprite
;     H is the height
;     L is the width
;82 bytes

;Set up the clipping
  call bigsprite_subroutine
  ret nc

bigsprite_Erase_loop:
  push bc   ;height,width
  push de   ;sprite data pointer
  push hl


spriteloop_Erase:
  push bc
  push de
  ld a,(de)
  cpl
  ld d,a
  ld e,-1
  ld a,(sprite_x)
  ld c,a
  and 7
  jr z,+_
  ld b,a
  ld a,e
  scf
  rr d \ rra \ djnz $-3
  ld e,a
_:
  ld a,c
  cp 96
  jr nc,+_
  ld a,(hl)
  and d
  ld (hl),a
  ld a,c
_:
  inc hl
  add a,8
  cp 96
  jr nc,+_
  ld a,(hl)
  and e
  ld (hl),a
_:
  ld de,11
  add hl,de
  ex (sp),hl
  ld a,(sprite_width)
  ld e,a
  add hl,de
  pop de
  ex de,hl
  pop bc
  djnz spriteloop_Erase


  pop hl
  inc hl
  ld a,(sprite_x)
  add a,8
  ld (sprite_x),a
  pop de
  inc de
  pop bc
  dec c
  jr nz,bigsprite_Erase_loop
  ret
